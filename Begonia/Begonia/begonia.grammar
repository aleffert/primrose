@start = module;
@symbols = '=' 'import' 'type' 'external' 'local' 'let' '#' ':' '(' ')' '<=' '{' '}' ',' 'fun' ',' ;
@allowsHexadecimalNotation = YES;
@allowsFloatingPoint = YES;

ident = /[A-Za-z][A-Za-z0-9_]*/ ;
opSymbol = (/[-+*$]/ | '/' )+;

scope = 'local' | 'let';

module = imports topDecls;

imports = import*;
import = 'import'! ident eols;

topDecls = (topDeclExternalType | topDeclValBinding | topDeclFunBinding | topDeclTypeBinding)*;

topDeclTypeBinding = 'type'! ident '='! datatypeArms eols!;
topDeclExternalType = 'external'! 'type'! ident eols!;
topDeclValBinding = scope ident '=' exp eols!;
topDeclFunBinding = scope ident bindingArguments '=' exp eols!;

datatypeArms = datatypeArm ('|'! datatypeArm)*;
datatypeArm = ident recordBinding;

bindingArguments = bindingArgument bindingArgument*;
bindingArgument = varBinding | recordBinding;

varBinding = '('! ident ':'! typeArgument ')'!;

recordBinding = '{'! eol* recordBindingFieldsOpt eol* '}'!;
recordBindingFieldsOpt = nullOpt | recordBindingFields;
recordBindingFields = recordBindingField (recordBreak! recordBindingField)*;
recordBindingField = ident ':'! typeArgument defaultValueOpt;

recordBreak = (','! eol*) | eols;

typeArgument = typeArgumentType;
typeArgumentType = type;

defaultValueOpt = nullOpt | '='! exp;
nullOpt = Empty;

exp = path+;

pathItem = ident;
projections = ('.'! pathItem)* ;

path = atom projections;

atom = expNum | expString | externalMethod | expVar | expRecord | expLambda | expStmt | '('! exp ')'!;
expNum = Number;
expString = QuotedString;
expVar = ident | opSymbol;
expLambda = 'fun'! bindingArguments '='! exp;
expStmt = '{'! ';'! eol* (stmt stmtBreak!)* eol* ';'! '}'! ;

expRecord = '{'! eol* expRecordFieldsOpt eol* '}'!;
expRecordFieldsOpt = expRecordFields | nullOpt;
expRecordFields = expRecordField (recordBreak! expRecordField)*;
expRecordField = ident (bindingArgument*) '='! exp;

externalMethod = '#'! exp '<=' exp ':' type;

stmt = stmtExp | stmtBind;
stmtExp = exp;
stmtBind = 'let'! ident bindingArgument* '='! exp;
stmtBreak = (';'! eol*) | eols;

type = typeVar;
typeVar = ident;

eols = eol+;
eol = '
'!;

